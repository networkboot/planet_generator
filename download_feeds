#!/usr/bin/env perl

use strict;
use warnings;
use autodie;
use feature qw(say);

use Text::CSV;
use XML::Feed;
use URI;
use LWP::Protocol::https;
use File::Slurp qw(write_file);
use Fcntl qw(:flock);

my $work_dir = 'work';

my $csv_file = 'feeds.csv';
my $csv_feed_url_column = 'feed_url';
my $csv_id_column       = 'md5';

chdir $work_dir;
my @contributors = get_contributors_from_csv();
download_feeds(@contributors);
exit;

############################

sub get_contributors_from_csv {
    say "Extracting contributors from $csv_file...";
    my $csv = Text::CSV->new({
        'sep_char' => ';',
        'auto_diag' => 1,
        'binary' => 1,
    });
    open my $csv_fh, '<', $csv_file;
    flock($csv_fh, LOCK_SH);
    my @columns = @{ $csv->getline($csv_fh) };
    $csv->column_names(@columns);
    my @contributors;
    while ( my $row = $csv->getline_hr($csv_fh) ) {
        push @contributors, {
            'feed_url' => $row->{ $csv_feed_url_column },
            'id'       => $row->{ $csv_id_column       },
        };
    }
    flock($csv_fh, LOCK_UN);
    close $csv_fh;
    return @contributors;
}

sub download_feeds {
    my (@contributors) = @_;
    say "Downloading feeds...";
    my @feeds;
    while ( my $contributor = shift @contributors) {
        my $feed_url = $contributor->{'feed_url'};
        my $id       = $contributor->{'id'};
        next unless $feed_url;
        next unless $id;
        say "Downloading $feed_url...";
        my $feed = eval { XML::Feed->parse( URI->new($feed_url) ); };
        warn "Unable to parse feed: $@" if $@;
        next if $@;
        say "\tUnable to find $feed_url" unless $feed;
        say "\tError occured: " . XML::Feed->error if XML::Feed->error;
        next unless $feed;
        say "\tTitle: " . $feed->title . " (" . $feed->format . ")";
        say "\tSaving to file: $id.xml";
        my $feed_xml = eval { $feed->as_xml; };
        warn "Unable to convert feed to XML: $@" if $@;
        next if $@;
        write_file( "$id.xml", { binmode => ':raw', atomic => 1 }, $feed_xml );
    }
}
